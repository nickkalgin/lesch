allprojects {
    group = 'org.lesch'
    version = '0.1.0-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    dependencies {
        testImplementation 'org.mockito:mockito-core:5.19.0'
        testImplementation platform('org.junit:junit-bom:5.13.4')
        testImplementation 'org.junit.jupiter:junit-jupiter'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                name = 'GitHubPackages'
                url = uri 'https://maven.pkg.github.com/nickkalgin/lesch'
                credentials {
                    username = project.findProperty('github.user')
                    password = project.findProperty('github.token')
                }
            }
        }
        publications {
            library(MavenPublication) {
                from components.java
            }
        }
    }

    tasks.withType(Jar) {
        archiveBaseName.set("${project.group}.${project.name}")
        archiveFileName.set("${archiveBaseName.get()}-${project.version}.jar")
    }

    tasks.withType(Test) {
        useJUnitPlatform()

        testLogging {
            events 'passed'
        }
    }
}

